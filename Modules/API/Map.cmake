include_guard(GLOBAL)

function (map action target)
  get_property(actions GLOBAL PROPERTY IXM_MAP_ACTIONS)
  if (NOT action IN_LIST actions)
    error("'map(${action})' is not a valid map operation")
  endif()
  get_property(command GLOBAL PROPERTY IXM_MAP_${action})
  if (NOT COMMAND ${command})
    error("'map(${action})' tried to execute an invalid or unknown command")
  endif()
  if (NOT TARGET ${target})
    add_library(${target} INTERFACE IMPORTED)
  endif()
  invoke(${command} ${target} ${ARGN})
endfunction()

function (ixm_map_contains target key out-var)
  get_property(out TARGET ${target} PROPERTY INTERFACE_${key} SET)
  set(${out-var} ${out} PARENT_SCOPE)
endfunction()

function (ixm_map_append target key)
  if (NOT ARGN)
    error("map(APPEND) requires at least one value to be appended")
  endif()
  set_property(TARGET ${target} APPEND PROPERTY INTERFACE_${key} ${ARGN})
endfunction()

function (ixm_map_assign target key)
  if (NOT ARGN)
    error("map(ASSIGN) requires at least one value to be assigned")
  endif()
  set_property(TARGET ${target} PROPERTY INTERFACE_${key} ${ARGN})
endfunction()

function (ixm_map_remove target)
  if (NOT ARGN)
    error("map(REMOVE) requires at least one key to be removed")
  endif()
  foreach (key IN LISTS ARGN)
    set_property(TARGET ${target} PROPERTY INTERFACE_${key})
  endforeach()
endfunction()

function (ixm_map_get target key var)
  get_property(out TARGET ${target} PROPERTY INTERFACE_${key})
  set(${var} ${out} PARENT_SCOPE)
endfunction()


