include_guard(GLOBAL)

function (glob out-var)
  parse(${ARGN} @FLAGS FILES_ONLY @ARGS=? INCLUDE EXCLUDE RELATIVE_TO)
  if (DEFINED INCLUDE AND DEFINED EXCLUDE)
    error("Cannot use INCLUDE and EXCLUDE regex parameters")
  endif()
  if (RELATIVE_TO)
    list(APPEND args RELATIVE "${RELATIVE_TO}")
  endif()
  set(list-directories ON)
  if (FILES_ONLY)
    set(list-directories OFF)
  endif()
  list(APPEND args LIST_DIRECTORIES ${list-directories})
  file(GLOB entries ${args} CONFIGURE_DEPENDS ${REMAINDER})
  if (INCLUDE OR EXCLUDE)
    var(regex INCLUDE "${EXCLUDE}")
    set(operation EXCLUDE)
    if (INCLUDE)
      set(operation INCLUDE)
    endif()
    list(FILTER entries ${operation} REGEX "${regex}")
  endif()
  set(${out-var} ${entries} PARENT_SCOPE)
endfunction()
