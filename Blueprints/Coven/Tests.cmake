include_guard(GLOBAL)

function (coven_tests_init)
  string(MAKE_C_IDENTIFIER "${PROJECT_NAME}" project)
  string(TOUPPER "${project}" project)
  if (NOT ${project}_BUILD_TESTS)
    return()
  endif()
  glob(items "${PROJECT_SOURCE_DIR}/tests/*")
  foreach (item IN LISTS items)
    if (IS_DIRECTORY "${item}")
      glob(files FILES_ONLY "${item}/*")
      if (NOT files)
        continue()
      endif()
    else()
      list(APPEND files "${item}")
    endif()
    get_filename_component(name "${item}" NAME_WE)
    string(REPLACE " " "-" name "${name}")
    set(target ${PROJECT_NAME}-test-${name})
    set(alias ${PROJECT_NAME}::${component}::${name})
    add_executable(${target} ${files})
    add_executable(${alias} ALIAS ${target})
    add_test(${alias} ${target})
    target_link_libraries(${target}
      PRIVATE
        $<TARGET_NAME_IF_EXISTS:${PROJECT_NAME}::${PROJECT_NAME}>)
    set_property(TARGET ${target} PROPERTY
      RUNTIME_OUTPUT_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}/tests)
  endforeach()
endfunction()
